//
// This file is to be applied to every subproject.
//

apply plugin: 'java'
apply plugin: 'maven-publish'

String mavenGroupId = 'flowpro'
String mavenVersion = '1.0-SNAPSHOT'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral();
}

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.
    testImplementation group: 'junit', name: 'junit', version: '4.10'
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = mavenGroupId
            artifactId = mavenArtifactId
            version = mavenVersion

            from components.java
        }
    }
    repositories {
        maven {
            url = uri("${buildDir}/repo")
        }
    }
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
